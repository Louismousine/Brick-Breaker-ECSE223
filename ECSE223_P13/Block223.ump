namespace ca.mcgill.ecse223.block.model;

class Block223 {
   1 <@>- * User users;
   1 <@>- * UserRole roles;
   1 <@>- * Game games;
   
   public Game findGame(String gameName) {
	  Game foundGame = null;
	  for(Game game: Block223Application.getBlock223().getGames()) {
		  if(game.getName().equals(gameName)) {
			  foundGame = game;
			  break;
		  }
	  }
	return foundGame;
  }   
}

class User {
   unique username;
   
   before constructor {
   if(aUsername.equals("") || aUsername == null)
     throw new RuntimeException("The username must be specified.");
   }
   
   1 -> 1..2 UserRole roles;
}

class UserRole {
   abstract;
   password;
   
   before constructor {
   if(aPassword.equals("") || aPassword == null)
     throw new RuntimeException("The player password needs to be specified.");
   }
}

class Admin {
   isA UserRole;
}

class Player {
   isA UserRole;
}

class Game {
   const Integer MIN_NR_LEVELS = 1;
   const Integer MAX_NR_LEVELS = 99; // this is somewhat redundant because the max multiplicity is enforced by Umple
   const Integer PLAY_AREA_SIDE = 400; // play area is now constant
   const Integer WALL_PADDING = 10;
   const Integer COLUMNS_PADDING = 5;
   const Integer ROW_PADDING = 2;
   unique name;
   Integer nrBlocksPerLevel;
   
    before constructor{
	if(name.equals("") || name == null){
		throw new RuntimeException("The name of a game must be specified.");
		}
	}
	before setNrBlocksPerLevel{
	if(aNrBlocksPerLevel<1){
		throw new RuntimeException("The number of blocks must be greater than 0");
		}
	}
   
   Block findBlock(int id) {
   		List<Block> blocks = this.getBlocks();
   		
   		for(Block block : blocks) {
   			int blockId =  block.getId();
   			
   			if(id == blockId) {
   				return block;
   			}
   		}
   		return null;
   }
   
   * -- 1 Admin admin;
   1 <@>- * Block blocks;
   1 <@>- 1..99 Level levels;
   1 <@>- * BlockAssignment blockAssignments;
   1 <@>- 1 Ball ball;
   1 <@>- 1 Paddle paddle;
}

class Block {
   const Integer MIN_COLOR = 0;
   const Integer MAX_COLOR = 255;
   const Integer MIN_POINTS = 1;
   const Integer MAX_POINTS = 1000; 
   const Integer SIZE = 20;
   autounique id;
   Integer red;
   Integer green;
   Integer blue;
   Integer points;
   
   before setRed {
   	
   	if (red < 0 || red > 255) {
   	throw new RuntimeException("Red must be between 0 and 255");
   	}
   
   }
   
   before setGreen {
   	
   	if (green < 0 || green > 255) {
   	throw new RuntimeException("Green must be between 0 and 255");
   	}
   
   }
   
   before setBlue {
   
   	if (blue < 0 || blue > 255) {
   		throw new RuntimeException("Blue must be between 0 and 255");
   	}
   	
   }
   
   before setPoints {
   
   	if (points < 1 || points > 1000) {
   		throw new RuntimeException("Points must be between 1 and 1000");
   	}
   }
}

// random attribute not needed anymore
// Each level is filled up with random blocks just before playing the level to reach the nrBlocksPerLevel defined in Game
class Level {
	BlockAssignment findBlockAssignment(int gridHorizontalPosition, int gridVerticalPosition) {
   		List<BlockAssignment> assignments = this.getBlockAssignments();
   		
   		for(BlockAssignment assignment : assignments) {
   			int h = assignment.getGridHorizontalPosition();
   			int v = assignment.getGridVerticalPosition();
   			
   			if(h == gridHorizontalPosition && v == gridVerticalPosition) {
   				return assignment;
   			}
   		}
   		return null;
   }
}

class BlockAssignment {
   Integer gridHorizontalPosition;
   Integer gridVerticalPosition;
   
   before constructor {
   
   if (aGridHorizontalPosition <= 0 || aGridHorizontalPosition > (1+(Game.PLAY_AREA_SIDE-2*Game.WALL_PADDING-Block.SIZE)/(Block.SIZE+Game.ROW_PADDING)))
   	 throw new RuntimeException("The horizontal position must be between 1 and "+ (1+(Game.PLAY_AREA_SIDE-2*Game.WALL_PADDING-Block.SIZE)/(Block.SIZE+Game.ROW_PADDING))+".");
   
   if (aGridVerticalPosition <= 0 || aGridVerticalPosition > (Game.PLAY_AREA_SIDE-2*Game.WALL_PADDING-Block.SIZE)/(Block.SIZE+Game.ROW_PADDING))
   	 throw new RuntimeException("The vertical position must be between 1 and "+ (Game.PLAY_AREA_SIDE-2*Game.WALL_PADDING-Block.SIZE)/(Block.SIZE+Game.ROW_PADDING)+".");
   }
   
 
   before setGridHorizontalPosition {
   if (aGridHorizontalPosition <= 0 || aGridHorizontalPosition > (1+(Game.PLAY_AREA_SIDE-2*Game.WALL_PADDING-Block.SIZE)/(Block.SIZE+Game.ROW_PADDING)))
   	 throw new RuntimeException("The horizontal position must be between 1 and "+ (1+(Game.PLAY_AREA_SIDE-2*Game.WALL_PADDING-Block.SIZE)/(Block.SIZE+Game.ROW_PADDING))+".");
   }
   
   before setGridVerticalPosition {
   if (aGridVerticalPosition <= 0 || aGridVerticalPosition > (Game.PLAY_AREA_SIDE-2*Game.WALL_PADDING-Block.SIZE)/(Block.SIZE+Game.ROW_PADDING))
   	 throw new RuntimeException("The vertical position must be between 1 and "+ (Game.PLAY_AREA_SIDE-2*Game.WALL_PADDING-Block.SIZE)/(Block.SIZE+Game.ROW_PADDING)+".");
   }
   
   * -- 1 Level level;
   * -- 1 Block block;
}

class Ball {
   const Integer BALL_DIAMETER = 10;
   Integer minBallSpeedX;
   Integer minBallSpeedY;
   Double ballSpeedIncreaseFactor;
   
   before setMinBallSpeedX{
   if(aMinBallSpeedX<1){
   	throw new RuntimeException("The minimum speed must be greater than 0");
   }
   }
   before setMinBallSpeedY{
   if(aMinBallSpeedY<1){
   	throw new RuntimeException("The minimum speed must be greater than 0");
   }
   }
   before setBallSpeedIncreaseFactor{
   if(!(aBallSpeedIncreaseFactor>0)){
   	throw new RuntimeException("The increase factor must be greater than 0");
   }
   }
}

class Paddle {
   const Integer PADDLE_WIDTH = 5;
   const Integer VERTICAL_DISTANCE = 30;
   Integer maxPaddleLength;
   Integer minPaddleLength;
   
   before setMinPaddleLength{
   if(aMinPaddleLength<1){
   	throw new RuntimeException("The minimum paddle length must be greater than 0");
   }
   }
   before setMaxPaddleLength{
   if(aMaxPaddleLength<1 || aMaxPaddleLength>400){
   	throw new RuntimeException("The maximum paddle length must be greater than 0 and no greater than 400");
   }
   }
}
